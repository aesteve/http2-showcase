import java.nio.file.Paths

plugins {
	id 'java'
	id 'application'
}

def alpnVersion = '8.1.7.v20160121'

repositories {
	mavenLocal()
	mavenCentral()
}

def repoPath = Paths.get repositories.mavenLocal().url
def alpnJar = new File("${repoPath}/org/mortbay/jetty/alpn/alpn-boot/${alpnVersion}/alpn-boot-${alpnVersion}.jar")

// TODO : create a task "checkAlpnJar" and run.dependsOn(checkAlpnJar)
// no need to check when compiling
if (!alpnJar.exists()) {
	throw new GradleScriptException("ALPN jar $alpnJar not found")
}

dependencies {
	compile 'io.vertx:vertx-core:3.3.0-SNAPSHOT'
	compile 'io.vertx:vertx-web:3.3.0-SNAPSHOT'

	runtime "org.mortbay.jetty.alpn:alpn-project:$alpnVersion"
}

mainClassName = 'io.vertx.core.Launcher'
def mainVerticle = 'io.vertx.examples.http2.Http2ServerVerticle'
applicationDefaultJvmArgs = [ "-Xbootclasspath/p:$alpnJar.absolutePath"]

run {
	args = ['run', mainVerticle]

}

task wrapper(type: Wrapper) {
	gradleVersion = '2.11'
}
